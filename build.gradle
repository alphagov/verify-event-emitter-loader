plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'java'

group 'uk.gov.verify'
version '1.0-SNAPSHOT'

def dependencyVersions = [
        dropwizard:"1.3.5",
        event_emmiter: "2.0.0-83"
]

repositories {
    if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
        logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/allowed-repos for production builds.\n\n')
        mavenCentral()
    }
    else {
        maven { url 'https://gds.jfrog.io/artifactory/allowed-repos' }
    }
}

dependencies {
    testCompile 'junit:junit:4.12',
            'org.assertj:assertj-core:1.6.0',
            'org.assertj:assertj-joda-time:1.1.0',
            'org.assertj:assertj-guava:1.1.0',
            'org.mockito:mockito-core:2.23.0',
            'org.json:json:20170516',
            'io.dropwizard:dropwizard-testing:' + dependencyVersions.dropwizard


    compile 'io.dropwizard:dropwizard-core:' + dependencyVersions.dropwizard,
            'io.dropwizard:dropwizard-client:' + dependencyVersions.dropwizard,
            'io.dropwizard:dropwizard-auth:' + dependencyVersions.dropwizard,
            'io.dropwizard:dropwizard-configuration:' + dependencyVersions.dropwizard,
            'com.google.inject:guice:4.0',
            'com.hubspot.dropwizard:dropwizard-guicier:1.0.0.6',
            'org.bouncycastle:bcprov-ext-jdk15on:1.58',
            'com.amazonaws:aws-encryption-sdk-java:1.3.1',
            'com.amazonaws:aws-java-sdk-kms:1.11.277',
            'uk.gov.ida:verify-event-emitter:' + dependencyVersions.event_emmiter,
            'javax.xml.bind:jaxb-api:2.3.1',
            'com.sun.xml.bind:jaxb-core:2.3.0.1',
            'com.sun.xml.bind:jaxb-impl:2.3.0.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'uk.gov.verify.eventsystem.loader.EventLoaderApplication'
    }

    // Create a fat jar, include dependencies!
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

apply from: "inttest.gradle"

tasks.check.dependsOn(intTest)
